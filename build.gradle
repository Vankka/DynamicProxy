plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
}

dependencies {
    api(project(':dynamicproxy-runtime'))
    implementation 'com.github.javaparser:javaparser-symbol-solver-core:3.25.9'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testAnnotationProcessor rootProject
}

allprojects {
    apply plugin: 'java-library'

    group 'dev.vankka'
    version '1.0.1-SNAPSHOT'

    targetCompatibility = JavaVersion.VERSION_1_8
    sourceCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    test {
        useJUnitPlatform()
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = project.name
            from components.java

            pom {
                name = project.name
                packaging = 'jar'
                description = 'A helper library for creating dynamic proxies'
                url = 'https://github.com/Vankka/DynamicProxy'

                scm {
                    connection = 'scm:git:https://github.com/Vankka/DynamicProxy.git'
                    developerConnection = 'scm:git:https://github.com/Vankka/DynamicProxy.git'
                    url = 'https://github.com/Vankka/DynamicProxy'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id = 'Vankka'
                    }
                }
            }
        }
    }
}

if (System.getenv('SIGNING_KEY') != null || project.hasProperty("signing.keyId")) {
    signing {
        sign publishing.publications.maven

        def key = System.getenv('SIGNING_KEY')
        if (key != null) {
            useInMemoryPgpKeys(key, System.getenv('SIGNING_KEY_PASS'))
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri('https://s01.oss.sonatype.org/service/local/'))
            snapshotRepositoryUrl.set(uri('https://s01.oss.sonatype.org/content/repositories/snapshots/'))
        }
    }
}