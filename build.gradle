plugins {
    id 'net.kyori.indra.licenser.spotless' version '2.2.0' apply false
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'net.kyori.indra.licenser.spotless'

    group 'dev.vankka'
    version '1.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
        withJavadocJar()
        withSourcesJar()
    }

    test {
        useJUnitPlatform()
    }

    indraSpotlessLicenser {
        licenseHeaderFile(rootProject.file('buildscript/LICENSE_HEADER'))
        newLine(true)
        property('inception', '2022')
        property('year_now', String.valueOf(Calendar.getInstance().get(Calendar.YEAR)))
    }

    publishing {
        publications {
            maven(MavenPublication) {
                artifactId = project.name
                from components.java

                pom {
                    name = project.name
                    packaging = 'jar'
                    description = 'A helper library for creating dynamic proxies'
                    url = 'https://github.com/Vankka/DynamicProxy'

                    scm {
                        connection = 'scm:git:https://github.com/Vankka/DynamicProxy.git'
                        developerConnection = 'scm:git:https://github.com/Vankka/DynamicProxy.git'
                        url = 'https://github.com/Vankka/DynamicProxy'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://www.opensource.org/licenses/mit-license.php'
                        }
                    }

                    developers {
                        developer {
                            id = 'Vankka'
                        }
                    }
                }
            }
        }
    }

    if (System.getenv('SIGNING_KEY') != null || project.hasProperty("signing.keyId")) {
        signing {
            sign publishing.publications.maven

            def key = System.getenv('SIGNING_KEY')
            if (key != null) {
                useInMemoryPgpKeys(key, System.getenv('SIGNING_KEY_PASS'))
            }
        }
    }
}

dependencies {
    api(project(':dynamicproxy-runtime'))
    implementation 'com.github.javaparser:javaparser-symbol-solver-core:3.26.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testAnnotationProcessor rootProject
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}